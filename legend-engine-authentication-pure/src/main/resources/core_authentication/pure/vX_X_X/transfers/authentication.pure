// Copyright 2021 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

###Pure
import meta::pure::extension::*;
import meta::pure::functions::collection::*;
import meta::pure::authentication::*;
import meta::protocols::pure::vX_X_X::extension::authentication::*;

function meta::protocols::pure::vX_X_X::transformation::fromPureGraph::authentication::transformAuthenticationSpec(a:meta::pure::authentication::AuthenticationSpec[1], extensions:Extension[*]):meta::protocols::pure::vX_X_X::metamodel::authentication::AuthenticationSpec[1]
{
   $a->match($extensions.serializerExtension('vX_X_X')->cast(@meta::protocols::pure::vX_X_X::extension::SerializerExtension_vX_X_X).moduleSerializerExtension('authenticationSpec')->cast(@meta::protocols::pure::vX_X_X::extension::authentication::AuthenticationModuleSerializerExtension).transfers_authentication_transformAuthenticationSpec->concatenate([
      o:meta::pure::authentication::OauthAuthenticationSpec [1] |
        ^meta::protocols::pure::vX_X_X::metamodel::authentication::OauthAuthenticationSpec(
            _type = 'oauth',
            grantType = $o.grantType->toString(),
            clientId = $o.clientId,
            clientSecretVaultReference = $o.clientSecretVaultReference,
            authServerUrl = $o.authServerUrl
        ),
      u:meta::pure::authentication::UsernamePasswordAuthenticationSpec [1] |
                ^meta::protocols::pure::vX_X_X::metamodel::authentication::UsernamePasswordAuthenticationSpec(
                    _type = 'basic',
                    username = $u.username,
                    password = ^meta::protocols::pure::vX_X_X::metamodel::authentication::VaultCredential(
                                           _type = 'vault',
                                           vaultReference = $u.password.vaultReference
                                           )
                ),
      a:meta::pure::authentication::ApiKeyAuthenticationSpec [1] |
                ^meta::protocols::pure::vX_X_X::metamodel::authentication::ApiKeyAuthenticationSpec(
                      _type = 'apiKey',
                      location = $a.location->toString(),
                      keyName = $a.keyName,
                      value = $a.value
                ),
      other: meta::pure::authentication::AuthenticationSpec[1] |
         ^meta::protocols::pure::vX_X_X::metamodel::authentication::AuthenticationSpec(
            _type = 'unknown'
         )
   ])->toOneMany())
}